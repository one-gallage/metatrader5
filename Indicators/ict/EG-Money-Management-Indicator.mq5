//+------------------------------------------------------------------+
//|                                EG-Money-Management-Indicator.mq4 |
//|                                         Copyright 2021. ErangaG  |
//|                         @author ErangaG. http://www.simply.trade |
//|                                                                  |
//+------------------------------------------------------------------+

#property copyright "Copyright 2021, ErangaG"
#property link      "http://www.simply.trade"
#property version   "21.11"
#property strict


enum EMyCapitalCalculation {
   FREEMARGIN = 2,
   BALANCE = 4,
   EQUITY = 8,
};

enum EMyRiskCalculation {
   ATR_POINTS = 3,
   FIXED_POINTS = 9,
};

class CMyToolkit {

 protected:

   virtual void  _Name() = NULL;   // A pure virtual function to make this class abstract

 public:

   static double NormalizeLots(string pSymbol, double pLots) {
      double uvolumeStep = SymbolInfoDouble(pSymbol, SYMBOL_VOLUME_STEP);
      double ulots = MathRound(pLots / uvolumeStep) * uvolumeStep; //-- normallize to a multiple of lotstep accepted by the broker
      return ulots;
   }

   static double ToPointDecimal(string pSymbol, uint pPointsCount) {
      int udigits = (int)SymbolInfoInteger(pSymbol, SYMBOL_DIGITS);
      double upointDecimal = SymbolInfoDouble(pSymbol, SYMBOL_POINT);
      return NormalizeDouble(upointDecimal * pPointsCount, udigits);
   }

   static int ToPointsCount(string pSymbol, double pDecimalValue) {
      double upointDecimal = SymbolInfoDouble(pSymbol, SYMBOL_POINT);
      return (int)((1 / upointDecimal) * pDecimalValue);
   }

   static int ToTicksCount(string pSymbol, uint pPointsCount) {
      // https://forum.mql4.com/43064#515262 for non-currency DE30:
      // SymbolInfoDouble(chart.symbol, SYMBOL_TRADE_TICK_SIZE) returns 0.5
      // SymbolInfoInteger(chart.symbol,SYMBOL_DIGITS) returns 1
      // SymbolInfoInteger(chart.symbol,SYMBOL_POINT) returns 0.1
      // Prices to open must be a multiple of ticksize
      double uticksize = SymbolInfoDouble(pSymbol, SYMBOL_TRADE_TICK_SIZE);
      int utickscount = (int)((pPointsCount / uticksize) * uticksize); //-- fix prices by ticksize
      return utickscount;
   }

   static double _CurrencyMultiplicator(string pCurrencyPairAppendix = "") {
      double _multiplicator = 1.0;
      string xCurrency = AccountInfoString(ACCOUNT_CURRENCY);
      StringToUpper(xCurrency);

      if(xCurrency == "USD")
         return (_multiplicator);
      if(xCurrency == "EUR")
         _multiplicator = 1.0 / SymbolInfoDouble("EURUSD" + pCurrencyPairAppendix, SYMBOL_BID);
      if(xCurrency == "GBP")
         _multiplicator = 1.0 / SymbolInfoDouble("GBPUSD" + pCurrencyPairAppendix, SYMBOL_BID);
      if(xCurrency == "AUD")
         _multiplicator = 1.0 / SymbolInfoDouble("AUDUSD" + pCurrencyPairAppendix, SYMBOL_BID);
      if(xCurrency == "NZD")
         _multiplicator = 1.0 / SymbolInfoDouble("NZDUSD" + pCurrencyPairAppendix, SYMBOL_BID);
      if(xCurrency == "CHF")
         _multiplicator = SymbolInfoDouble("USDCHF" + pCurrencyPairAppendix, SYMBOL_BID);
      if(xCurrency == "JPY")
         _multiplicator = SymbolInfoDouble("USDJPY" + pCurrencyPairAppendix, SYMBOL_BID);
      if(xCurrency == "CAD")
         _multiplicator = SymbolInfoDouble("USDCAD" + pCurrencyPairAppendix, SYMBOL_BID);
      if(_multiplicator == 0)
         _multiplicator = 1.0; // If account currency is neither of EUR, GBP, AUD, NZD, CHF, JPY or CAD we assumes that it is USD
      return (_multiplicator);
   }


   static double CalculateLotSize(string pSymbol, double pMoneyCapital, double pRiskPercentage, int pStoplossPoints, int pExtraPriceGapPoints, double pAllowedMaxLotSize, string pCurrencyPairAppendix = "") {
      // Calculate LotSize based on Equity, Risk in decimal and StopLoss in points
      double _moneyRisk, _lotsByRequiredMargin, _lotsByRisk, _lotSize;
      int _lotdigit = 2, _totalSLPoints, _totalTickCount;

      // Calculate Lot size according to Equity.
      double _marginForOneLot;
      if(OrderCalcMargin(ORDER_TYPE_BUY, pSymbol, 1, SymbolInfoDouble(pSymbol, SYMBOL_ASK), _marginForOneLot)) { // Calculate margin required for 1 lot
         _lotsByRequiredMargin = pMoneyCapital * 0.98 / _marginForOneLot;
         _lotsByRequiredMargin = MathMin(_lotsByRequiredMargin, MathMin(pAllowedMaxLotSize, SymbolInfoDouble(pSymbol, SYMBOL_VOLUME_MAX)));
         _lotsByRequiredMargin = NormalizeLots(pSymbol, _lotsByRequiredMargin);
      } else {
         _lotsByRequiredMargin = SymbolInfoDouble(pSymbol, SYMBOL_VOLUME_MAX);
      }

      double _lotStep = SymbolInfoDouble(pSymbol, SYMBOL_VOLUME_STEP); // Step in lot size changing
      double _oneTickValue = SymbolInfoDouble(pSymbol, SYMBOL_TRADE_TICK_VALUE); // Tick value of the asset

      if(_lotStep ==  1) _lotdigit = 0;
      if(_lotStep == 0.1) _lotdigit = 1;
      if(_lotStep == 0.01) _lotdigit = 2;

      _moneyRisk = (pRiskPercentage/100) * pMoneyCapital;
      _totalSLPoints = pStoplossPoints + pExtraPriceGapPoints;
      _totalTickCount = ToTicksCount(pSymbol, _totalSLPoints);

      // Calculate the Lot size according to Risk.
      _lotsByRisk = _moneyRisk / (_totalTickCount * _oneTickValue);
      _lotsByRisk = _lotsByRisk * _CurrencyMultiplicator(pCurrencyPairAppendix);
      _lotsByRisk = NormalizeLots(pSymbol, _lotsByRisk);

      _lotSize = MathMax(MathMin(_lotsByRisk, _lotsByRequiredMargin), SymbolInfoDouble(pSymbol, SYMBOL_VOLUME_MIN));
      _lotSize = NormalizeDouble(_lotSize, _lotdigit);
      return (_lotSize);
   }

   static void DisplayText(string objname, string objtext, int clr, int x, int y, int corner) {
      if(ObjectFind(ChartID(), objname) == -1) {
         ObjectCreate(ChartID(), objname, OBJ_LABEL, 0, 0, 0);
         ObjectSetInteger(ChartID(), objname, OBJPROP_CORNER, corner);
         ObjectSetInteger(ChartID(), objname, OBJPROP_XDISTANCE, x);
         ObjectSetInteger(ChartID(), objname, OBJPROP_YDISTANCE, y);
         ObjectSetInteger(ChartID(), objname, OBJPROP_FONTSIZE, 13);
         ObjectSetString(ChartID(), objname, OBJPROP_FONT, "Arial");
      }
      ObjectSetString(ChartID(), objname, OBJPROP_TEXT, objtext);
      ObjectSetInteger(ChartID(), objname, OBJPROP_COLOR, clr);
   }

};


#property indicator_chart_window
#property indicator_buffers 4
#property indicator_plots   3
#property indicator_type1   DRAW_NONE
#property indicator_color1  clrNONE
#property indicator_type2   DRAW_NONE
#property indicator_color2  clrNONE
#property indicator_type3   DRAW_NONE
#property indicator_color3  clrNONE

input group                   "Risk Mode"
input EMyCapitalCalculation   iRisk_AvailableCapital = BALANCE;  // Capital calculation mechanism
input double                  iRisk_Percentage = 0.1;           // Risk Percentage; ex: 0.5 = 0.5%
input EMyRiskCalculation      iRisk_RiskMode = ATR_POINTS;       // Stop-Loss points calculation mechanism

input group       "Stop-Loss Calculation"
input int         iCommon_ATRLength = 10;                         // ATR length for ATR based Stop-Loss
input double      iCommon_ATRMultiplier = 3;                      // ATR value multiplier
input int         iCommon_FixedStoplossPoints = 300;             // Fixed size Stop-Loss point count

input group       "General Settings"
input string      iCommon_CurrencyPairAppendix = "";              // Currency Pair Appendix
input color       iCommon_ColorParameters = clrChocolate;            // Colour for Parameters
input color       iCommon_ColorLotsize = clrDeepSkyBlue;          // Colour for calculaed Lots

ENUM_BASE_CORNER  mDisplay_Corner = CORNER_LEFT_LOWER;
int mMaxPeriod;
int mHandleATR;

double   mBuffer_lotsize[];
double   mBuffer_stoplossPoints[];
double   mBuffer_atrPoints[];
double   mBuffer_atr[];

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
bool myIndicatorCreate() {
   mHandleATR = iATR(NULL, PERIOD_CURRENT, iCommon_ATRLength);
   if (mHandleATR == INVALID_HANDLE) return (false);
   return (true);
}

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
void myIndicatorRelease() {
   if (mHandleATR != INVALID_HANDLE) IndicatorRelease(mHandleATR);
}

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
int OnInit() {
   SetIndexBuffer(0, mBuffer_lotsize, INDICATOR_DATA);
   ArraySetAsSeries(mBuffer_lotsize,true);
   SetIndexBuffer(1, mBuffer_stoplossPoints, INDICATOR_DATA);
   ArraySetAsSeries(mBuffer_stoplossPoints,true);
   SetIndexBuffer(2, mBuffer_atrPoints, INDICATOR_DATA);
   ArraySetAsSeries(mBuffer_atrPoints,true);
   SetIndexBuffer(3, mBuffer_atr, INDICATOR_CALCULATIONS);
   ArraySetAsSeries(mBuffer_atr,true);

   PlotIndexSetString(0, PLOT_LABEL, "EG_Lots");
   PlotIndexSetString(1, PLOT_LABEL, "EG_Stoploss_Points");
   PlotIndexSetString(2, PLOT_LABEL, "EG_ATR_Points");

   mMaxPeriod = iCommon_ATRLength;
   PlotIndexSetInteger(0, PLOT_DRAW_BEGIN, mMaxPeriod);
   PlotIndexSetInteger(1, PLOT_DRAW_BEGIN, mMaxPeriod);
   PlotIndexSetInteger(2, PLOT_DRAW_BEGIN, mMaxPeriod);

   IndicatorSetInteger(INDICATOR_DIGITS, 2);
   IndicatorSetString(INDICATOR_SHORTNAME, MQLInfoString(MQL_PROGRAM_NAME));

   string str;
   if(myIndicatorCreate() == false) {
      StringConcatenate(str, "Failed to create indicator handle(s) , Error: ", GetLastError());
      MessageBox(str, "Error");
      return (INIT_FAILED);
   }
   return(INIT_SUCCEEDED);
}

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
void OnDeinit(const int  reason) {
   ObjectDelete(ChartID(), "@MM-ATR");
   ObjectDelete(ChartID(), "@MM-LotText");
   ObjectDelete(ChartID(), "@MM-LotSize");
   myIndicatorRelease();
}

//+------------------------------------------------------------------+
//|                                                                  |
//+------------------------------------------------------------------+
int OnCalculate(const int rates_total,
                const int prev_calculated,
                const datetime &time[],
                const double &open[],
                const double &high[],
                const double &low[],
                const double &close[],
                const long &tick_volume[],
                const long &volume[],
                const int &spread[]) {

   int to_copy,limit,i;
   
   if(prev_calculated>rates_total || prev_calculated<=0) {      
      limit=MathMin(rates_total-mMaxPeriod,20);       // starting index for calculation of all bars
   }
   else {
      limit=rates_total-prev_calculated; // starting index for calculation of new bars
   }

   to_copy = limit+3;

   if (CopyBuffer(mHandleATR, 0, 0, to_copy, mBuffer_atr) <= 0) return(0);

   double availableMoney, lotsize;
   int atrPoints, slPoints;
   string str;

   // Get available money
   switch (iRisk_AvailableCapital) {
   case FREEMARGIN:
      availableMoney = AccountInfoDouble(ACCOUNT_MARGIN_FREE);
      break;
   case BALANCE:
      availableMoney = AccountInfoDouble(ACCOUNT_BALANCE);
      break;
   case EQUITY:
      availableMoney = AccountInfoDouble(ACCOUNT_EQUITY);
      break;
   default:
      availableMoney = AccountInfoDouble(ACCOUNT_MARGIN_FREE);
      break;
   }

   for(i=limit; i>=0 && !IsStopped(); i--) {      

      atrPoints = (int)(mBuffer_atr[i] * MathPow(10, SymbolInfoInteger(NULL, SYMBOL_DIGITS)));
      slPoints = (int)MathCeil(iCommon_ATRMultiplier * atrPoints);

      // calculate raw lotsize
      if(iRisk_RiskMode == FIXED_POINTS) {
         slPoints = iCommon_FixedStoplossPoints;
      } else {
         slPoints = slPoints > 0 ? slPoints : iCommon_FixedStoplossPoints;
      }      
      
      slPoints += (int)SymbolInfoInteger(Symbol(),SYMBOL_SPREAD); // add the current spread

      lotsize = CMyToolkit::CalculateLotSize(NULL, availableMoney, iRisk_Percentage, slPoints, 0, SymbolInfoDouble(NULL, SYMBOL_VOLUME_MAX), iCommon_CurrencyPairAppendix);

      mBuffer_atrPoints[i] = StringToDouble(IntegerToString(atrPoints));
      mBuffer_stoplossPoints[i] = StringToDouble(IntegerToString(slPoints));
      mBuffer_lotsize[i] = StringToDouble(DoubleToString(lotsize, 2));;
   }

   if(iRisk_RiskMode == ATR_POINTS) {
      str = "";
      StringConcatenate(str, "ATR (", iCommon_ATRLength, ") : ", StringToInteger(DoubleToString(mBuffer_atrPoints[0])), " points");
      CMyToolkit::DisplayText("@MM-ATR", str, iCommon_ColorParameters, 30, 110, mDisplay_Corner);
   }

   str = "";
   StringConcatenate(str, "Risk : ", DoubleToString(iRisk_Percentage, 2), "% , Stop-Loss : ", StringToInteger(DoubleToString(mBuffer_stoplossPoints[0])), " points");
   CMyToolkit::DisplayText("@MM-LotText", str, iCommon_ColorParameters, 30, 80, mDisplay_Corner);
   str = "";
   StringConcatenate(str, "Lots : ", mBuffer_lotsize[0]);
   CMyToolkit::DisplayText("@MM-LotSize", str, iCommon_ColorLotsize, 30, 50, mDisplay_Corner);

   return(rates_total);
}

//+------------------------------------------------------------------+
